plugins {
    id 'java'
}

group 'com.samjakob'
version '1.3.0-SNAPSHOT'

java {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {
    mavenCentral()

    maven {
        // Sonatype
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    maven {
        // Spigot Nexus
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'

    // Spigot API
    implementation group: 'org.spigotmc', name: 'spigot-api', version: '1.8.8-R0.1-SNAPSHOT'
}

tasks.register('testJar', Jar) {
    from sourceSets.main.output.classesDirs
    from sourceSets.test.output.classesDirs
    from('src/test/resources') {
        include '**'
    }

    archiveBaseName.set("SpiGUITest")
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.java.sourceDirectories
}

tasks.register('classesJar', Jar) {
    from sourceSets.main.output.classesDirs
}

tasks.register('documentation', Javadoc) {
    source = sourceSets.main.java.sourceDirectories
    classpath += configurations.compileClasspath

    options.memberLevel = JavadocMemberLevel.PRIVATE
}

tasks.register('javadocJar', Jar) {
    dependsOn documentation
    archiveClassifier.set('javadoc')
    from documentation.destinationDir
}

artifacts {
    archives classesJar
    archives javadocJar
    archives sourcesJar
}
